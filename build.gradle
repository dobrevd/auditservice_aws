plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.dobrev'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

ext {
	lombokVersion = '1.18.30'
	mapstructVersion = '1.5.5.Final'
	awsSdkBomVersion = '2.29.37'
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-web'

	implementation 'org.springframework.boot:spring-boot-starter-log4j2'

	implementation platform("software.amazon.awssdk:bom:${awsSdkBomVersion}")
	implementation 'software.amazon.awssdk:dynamodb'
	implementation 'software.amazon.awssdk:dynamodb-enhanced'

	// AWS X-Ray
	implementation 'com.amazonaws:aws-xray-recorder-sdk-spring:2.18.2'
	implementation 'com.amazonaws:aws-xray-recorder-sdk-aws-sdk-v2:2.18.2'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Annotation Processors
	implementation "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
	implementation "org.mapstruct:mapstruct:${mapstructVersion}"
	annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"
}

tasks.named('test') {
	useJUnitPlatform()
}

configurations.configureEach {
	exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
	exclude group: 'org.apache.logging.log4j', module: 'log4j-to-slf4j'
	exclude group: 'commons-logging', module: 'commons-logging'
}

tasks.register("unpack", Copy) {
	dependsOn bootJar
	from(zipTree(tasks.bootJar.outputs.files.singleFile))
	into("build/libs")
}